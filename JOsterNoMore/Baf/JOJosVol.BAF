// Dead Biff

IF
	Dead("JOJoster")
	See([PC])
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@6300)
		EscapeArea()
END


// One Biff

IF
	Global("JOJosOneBiff","GLOBAL",1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@6203)	
		Wait(3)
		SetGlobal("JOJosOneBiff","GLOBAL",2)
END

IF
	Global("JOJosOneBiff","GLOBAL",8)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@6204)	
		Wait(3)
		SetGlobal("JOJosOneBiff","GLOBAL",9)
END

// Ama is dead

IF
	Global("JOJosterLathan","GLOBAL",1)
THEN
	RESPONSE #100
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@6101)
		Wait(3)
		SetGlobal("JOJosterLathan","GLOBAL",2)
		Continue()
END

IF
	Global("JOJosterLathan","GLOBAL",5)
THEN
	RESPONSE #100
		FaceObject("shthlt01")
		DisplayStringHead(Myself,@6105)
		SetGlobal("JOJosterLathan","GLOBAL",6)
		Wait(3)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END

// Travel

IF
	InMyArea([PC])
	InMyArea("JOJoster")
	Global("JOJosVolGoBackNow","GLOBAL",1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@3999)
		SetGlobal("JOJosterGoBackNow","GLOBAL",2)
		SetGlobal("JOJosVolGoBackNow","GLOBAL",0)
		SetGlobal("JOJosterGoBykNow","GLOBAL",0)
		SetGlobal("JOJosVolThieves","GLOBAL",2)
		MoveToPointNoInterrupt([710.185])
END

IF
	InMyArea([PC])
	!InMyArea("JOJoster")
	Global("JOJosterGoBackNow","GLOBAL",1) 
THEN
	RESPONSE #100
		SetGlobal("JOJosterGoBackNow","GLOBAL",2)
		SetGlobal("JOJosVolThieves","GLOBAL",2)
		Wait(2)
		DisplayStringHead(Myself,@3001)
		MoveToPointNoInterrupt([710.185])
END

// Not a idiot

IF
	CombatCounter(0)
	Global("JOJosVolAttack","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("JOJosVolAttack","GLOBAL",0)
		MoveToPointNoInterrupt([710.185])
		FaceObject("JOJoster")
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		//SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END

IF
	See(NearestEnemyOf(Player1))
	!Global("JOJosVolAttack","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		ApplySpell("JOJoster",INVOKE_COURAGE)
		ApplySpell("JOJosvol",CLERIC_REMOVE_FEAR)
		DisplayStringHead(Myself,@6304)
		Attack(NearestEnemyOf(Player1))
		SetGlobal("JOJosVolAttack","GLOBAL",1)
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		SetGlobal("JOJosterBiffIsHere","GLOBAL",3)
		ApplySpell("JOJoster",INVOKE_COURAGE)
		ApplySpell("JOJosvol",CLERIC_REMOVE_FEAR)
		DisplayStringHead(Myself,@6302)
		ActionOverride("JOJoster",DisplayStringHead(Myself,@6303))
		ActionOverride("JOJoster",EscapeArea())
		EscapeArea()
END

// Thieves are here


IF
	Global("PGThievesLeave","GLOBAL",0)
	Global("JOJosVolThieves","GLOBAL",0)
	Global("JOJosterGetPaid","GLOBAL",0)	
THEN
	RESPONSE #50
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		MoveToPointNoInterrupt([765.599])
		Wait(1)
		FaceObject("Varia")
		Wait(1)
		DisplayStringHead(Myself,@7000)
		Wait(1)
		ActionOverride("Varia",FaceObject("JOJosVol"))
		Wait(4)
		ActionOverride("Varia",DisplayStringHead(Myself,@7001))
		Wait(5)
		ActionOverride("Varia",DisplayStringHead(Myself,@7002))
		Wait(4)
		DisplayStringHead(Myself,@7003)
		SetGlobal("JOJosVolThieves","GLOBAL",1)
		Continue()
	RESPONSE #50
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		NoAction()
		SetGlobal("JOJosVolThieves","GLOBAL",1)
END
		
IF
	Global("PGThievesLeave","GLOBAL",0)
	Global("JOJosVolThieves","GLOBAL",1)
	Global("JOJosterGetPaid","GLOBAL",0)	
THEN
	RESPONSE #50
		Wait(20)
		MoveToPointNoInterrupt([958.416])
		DisplayStringHead(Myself,@7004) 
		ActionOverride("Milph",FaceObject("JOJosVol"))
		ActionOverride("Kretor",FaceObject("JOJosVol"))
		Wait(4)
		ActionOverride("Milph",DisplayStringHead(Myself,@7005)) // Goshan
		Wait(5)
		ActionOverride("Kretor",DisplayStringHead(Myself,@7006)) // Kretor
		SetGlobal("JOJosVolThieves","GLOBAL",2)
		Continue()
	RESPONSE #50	
		Wait(20)
		MoveToPointNoInterrupt([1068.383])
		FaceObject("Morsa")
		DisplayStringHead(Myself,@7007)
		ActionOverride("Morsa",FaceObject("JOJosVol"))
		Wait(4)
		ActionOverride("Morsa",DisplayStringHead(Myself,@7008))
		Wait(4)
		MoveToPointNoInterrupt([1067.559])
		EquipRanged()
		SetSequence(SEQ_HIDE)
		ActionOverride("Morsa",MoveToPointNoInterrupt([1067.559]))
		ActionOverride("Morsa",SetSequence(SEQ_HIDE))
		SetGlobal("JOJosVolThieves","GLOBAL",2)
		Wait(50)		
		MoveToPointNoInterrupt([1068.383])
		SetSequence(SEQ_READY)
		ActionOverride("Morsa",MoveToPointNoInterrupt([1068.383]))
		ActionOverride("Morsa",SetSequence(SEQ_READY))
		Continue()
END
				
IF
	Global("PGThievesLeave","GLOBAL",1)
	!Global("JOJosVolThieves","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("JOJosVolThieves","GLOBAL",0)
		ClearActions("JOJosVol")
		Wait(2)
		DisplayStringHead(Myself,@7009)
		MoveToPointNoInterrupt([710.185])
		Wait(6)
		SetGlobal("JOJosVolThieves","GLOBAL",0)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END


// Routine Link

IF
	Global("JOJosterSacreOff","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("JOJosterSacreOff","GLOBAL",0)
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4005)	
		Wait(2)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END

IF
	Global("JOJosterOffOff","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("JOJosterOffOff","GLOBAL",0)
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4014)	
		Wait(2)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END

IF
	Global("JOJosterWendyOff","GLOBAL",1)
THEN
	RESPONSE #100
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4006)
		Wait(2)
		SetGlobal("JOJosterWendyOff","GLOBAL",2)
END


// Routine / Jariel / Lathan

IF
	Global("JOJosterJarLath","GLOBAL",5)
THEN
	RESPONSE #100
		FaceObject("shthlt02")
		DisplayStringHead(Myself,@6003)	
		Wait(2)
		SetGlobal("JOJosterJarLath","GLOBAL",6)
END

IF
	Global("JOJosterJarLath","GLOBAL",7)
THEN
	RESPONSE #100
		FaceObject("shthlt01")
		DisplayStringHead(Myself,@6005)	
		Wait(3)
		SetGlobal("JOJosterJarLath","GLOBAL",0)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		Continue()
END


  // Routine / Rattell

IF
	Global("JOJosterRattell","GLOBAL",2)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@4018)	
		Wait(2)
		MoveToPointNoInterrupt([710.185])
		Wait(4)
		SetGlobal("JOJosterRattell","GLOBAL",3)
END

// Routine

/*
IF
  Global("JOJosVolShowOff","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobalTimer("JOJosVolShowOff","AR0322",ONE_MINUTE)
	SetGlobal("JOJosVolShowOff","GLOBAL",0)
END

IF
  GlobalTimerExpired("JOJosVolShowOff","GLOBAL")
THEN
  RESPONSE #100
	SetGlobal("JOJosVolShowOff","GLOBAL",0)
END
*/

IF
	Global("JOJosVolShowOff","GLOBAL",1)
	InMyArea("JOJoster")
THEN
	RESPONSE #100
		NoAction()
		Wait(18)
		SetGlobal("JOJosVolShowOff","GLOBAL",0)
		Continue()
END

IF
	AreaCheck("AR0322")
	Global("JOJosterBiffIsHere","GLOBAL",2)
	Global("JOJosVolShowOff","GLOBAL",0)
	See([PC])
	!Dead("JOJoster")
	!Global("JOJosterGoBackNow","GLOBAL",1)
	!Global("JOJosterGetPaid","GLOBAL",1)
THEN
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4000)
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		FaceObject(Player1)
		DisplayStringHead(Myself,@4001)
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4002)
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4003)
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		FaceObject("JOJoster")
		DisplayStringHead(Myself,@4004)	
//  RESPONSE #20
//	Wait(5)
//	SetGlobal("JOJosVolShowOff","GLOBAL",1)
//	DisplayStringHead(Myself,@4005)	
//  RESPONSE #20
//	Wait(5)
//	SetGlobal("JOJosVolShowOff","GLOBAL",1)
//	DisplayStringHead(Myself,@4006)	
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4007)	
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4008)	
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4009)	
	RESPONSE #20
  // Routine / Jariel / Lathan
		ClearActions("JOJoster")
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		Wait(2)
		DisplayStringHead(Myself,@4010)	
		Wait(3)
		SetGlobal("JOJosterJarLath","GLOBAL",1)	
	RESPONSE #20
		ClearActions("JOJoster")
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		Wait(2)
		DisplayStringHead(Myself,@4011)
		Wait(2)
		SetGlobal("JOJosterRenalOff","GLOBAL",2) // Renal 
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4012)	
	RESPONSE #20
  // Routine / Rattell
		ClearActions("JOJoster")
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		MoveToPointNoInterrupt([429.429])
		DisplayStringHead(Myself,@4017)	
		Wait(2)
		SetGlobal("JOJosterRattell","GLOBAL",1) // Rattell 
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4013)	
//  RESPONSE #20
//	Wait(5)
//	SetGlobal("JOJosVolShowOff","GLOBAL",1)
//	DisplayStringHead(Myself,@4014)	
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",1)
		DisplayStringHead(Myself,@4015)	
	RESPONSE #20
		SetGlobal("JOJosVolShowOff","GLOBAL",2) // Pause Routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",2) // Pause Routine JOJoster
		Wait(1)
		ActionOverride("JOJoster",DisplayStringHead(Myself,@4020))	
		Wait(2)	
		DisplayStringHead(Myself,@4019)		
		MoveToPointNoInterrupt([1138.436])	
		//ActionOverride("shthstor",DisplayStringHead(Myself,@xxx))		
		//ActionOverride("shthlt02",DisplayStringHead(Myself,@xxx))		
		//ActionOverride("shthlt01",DisplayStringHead(Myself,@xxx))	
		Wait(2)
		SetGlobal("JOJosterVolUpstair","GLOBAL",1)
		MoveBetweenAreas("AR0323",[140.591],S)
END

IF
	AreaCheck("AR0323")
	Global("JOJosterBiffIsHere","GLOBAL",2)
	See([GOODCUTOFF])
	!Dead("JOJoster")
	!GlobalGT("JOJosterGoBackNow","GLOBAL",0)
	!GlobalGT("JOJosterGetPaid","GLOBAL",0)
THEN
	RESPONSE #20
		MoveToPoint([250.626])
		Wait(3)
		MoveToPoint([259.493])
		Wait(3)
		FaceObject(LastSeenBy(Myself))
		DisplayStringHead(Myself,@4021)
		Wait(2)
		DisplayStringHead(Myself,@4022)	
		Wait(1)
		//ActionOverride(Player1,DisplayStringHead(Myself,@xxx))		
		//ActionOverride(Player2,DisplayStringHead(Myself,@xxx))		
		//ActionOverride(Player3,DisplayStringHead(Myself,@xxx))		
		//ActionOverride(Player4,DisplayStringHead(Myself,@xxx))		
		//ActionOverride(Player5,DisplayStringHead(Myself,@xxx))		
		//ActionOverride(Player6,DisplayStringHead(Myself,@xxx))		
		MoveToPointNoInterrupt([140.591])
		DisplayStringHead(Myself,@4023)	
		SetGlobal("JOJosterVolUpstair","GLOBAL",0)
		SetGlobal("JOJosVolThieves","GLOBAL",1)
		Wait(3)
		SetGlobal("JOJosVolShowOff","GLOBAL",1)  // Relaunch routine JOJosVol
		SetGlobal("JOJosterShowOff","GLOBAL",1) // Relaunch Routine JOJoster
		MoveBetweenAreas("AR0322",[710.185],S) // JOJosVol Spot
END



/*

Not for now

// Thief in Jail

IF
  Global("JOJosJail","GLOBAL",1)
THEN
  RESPONSE #100
	DisplayStringHead(Myself,@4016)
	SetGlobal("JOJosJail","GLOBAL",2)
	ClearAllActions()
	Continue()
END
*/
